Let's make a game!
  name:Tribal Ancients
  desc:You are your tribe's leader. Rule your tribe through times of starving, war, and natural disasters to ensure your tribe's survival.

Settings
  stylesheet:stuff/bigBlue.css
  spritesheet:icons, 48 by 48, https://raw.githubusercontent.com/CreativeCoder-GitHub/tribal-ancients/main/icons48.png
  tiling background:https://orteil.dashnet.org/cookieclicker/img/bgSky.jpg
  no bulk particles

CSS
  #box-upgrades
  {
    top: 43px;
  }
  .box-header, .box-footer 
  {
      padding: 8px;
      font-size: 15px;
      font-weight: bold;
  }

  #box-upgrBtns
  {
    position: relative;
    width: 50%;
    left: 50%;
    height: 43px;
  }

  .upgradeSwitch
  {
    width: 50%;
    height: 43px;
    margin: 0px;
    float: left;
  } 
  #box-achievs
  {
    position: fixed;
    bottom: 0;
    text-align: center;
  }

Resources
  *water
    name:Water
    desc:Your tribe needs water to drink.
    icon:icons[1,0]
  *fruit
    name:Fruit
    desc:Fruits are sweet and tasty. Your tribe loves eating these.
  *seed|seeds
    name:Seeds
    desc:Can grow into delicious fruit!
    start with:5
    icon:icons[2,0]
  *meat
    name:Meat
    desc:Meat has lots of protein, key to keeping your tribespeople healthy.
  *wood
    name:Wood
    desc:Wood can be used to create structures.
  *stone
    name:Stone
    desc:Stone can be used to create structures.
    icon:icons[0,0]
  *totalPopulation
    name:Tribesperson|Tribespeople
    desc:The total number of people in your tribe, including employed and unemployed.
    is always: (people + hunters + farmers + lumberjacks + miners)
  *person|people
    // Initial population is 5
    name:Unemployed Tribesperson|Unemployed Tribespeople
    desc:The amount of people available for work in your tribe.
    start with:5
  *fertilizerCost
    always hidden
    start with:10

Buttons
  *workBtn
    name:Perform Work
    desc:Use this button to manually do work for your tribe.
    icon:https://raw.githubusercontent.com/CreativeCoder-GitHub/tribal-ancients/main/bigButton.png
    no text
    no tooltip
    on click:yield random(0, 2) water
    on click:yield random(0, 2) fruit
    on click:yield random(0, 2) meat
    on click:yield random(0, 2) wood
    on click:yield random(0, 2) stone
    on click:anim icon wobble
    class:bigButton hasFlares
  *gainFertilizerBtn
    name:Buy Fertilizer
    desc:Click to buy fertilizer(cost <b>[fertilizerCost]</b> water, <b>[fertilizerCost]</b> fruit). Each fertilizer makes farmers <b>twice as efficient.</b>
    on click:yield 1 fertilizer
    on click:yield -1 * fertilizerCost water
    on click:yield -1 * fertilizerCost fruit
    on click:yield fertilizerCost * 2 fertilizerCost
  *showAvailUpgr
    name:Available Upgrades
    tag:upgradeSwitcher
    class:upgradeSwitch
    on click:
      show :Upgrades:notOwned
      hide :Upgrades:owned
      anim glow
    end
    no tooltip
  *showOwnedUpgr
    name:Owned Upgrades
    tag:upgradeSwitcher
    class:upgradeSwitch
    on click:
      hide :Upgrades:notOwned
      show :Upgrades:owned
      anim glow
    end
    no tooltip

Buildings
  *backgroundWorker
    start with:1
    always hidden
    on load:hide :Upgrades:owned
    on tick:yield 0.1 people
    on tick:
        yield (-3 * totalPopulation) water
        yield (-3 * totalPopulation) fruit
        yield (-3 * totalPopulation) meat
    end
    on tick:
      show gainFertilizerBtn
      if (ItemsLeft = 0) hide gainFertilizerBtn
      if (water < 5) hide gainFertilizerBtn
      if (fruit < 5) hide gainFertilizerBtn
      if (farmers < 25) hide gainFertilizerBtn
    end
  *well|wells
    name:Well|Wells
    desc:A source of water
    cost:5 stone,3 wood
    on tick:yield 5 water
  *hunter|hunters
    name:Hunter|Hunters
    cost refund:100%
    cost increase:100%
    cost:1 people
    desc:Hunters are people in the tribe who bring back meat.
    on tick:yield 10 meat
  *farmer|farmers
    name:Farmer|Farmers
    cost refund:100%
    cost increase:100%
    cost:1 people,5 seeds
    desc:Farmers grow fruits for your tribe.
    on tick:yield 10 fruit
    on tick:yield 10 seeds
  *lumberjack|lumberjacks
    name:Lumberjack|Lumberjacks
    cost refund:100%
    cost increase:100%
    cost:1 people
    desc:Lumberbacks cut down wood for your tribe.
    on tick:yield 10 wood
  *miner|miners
    name:Miner|Miners
    cost refund:100%
    cost increase:100%
    cost:1 people
    desc:Miners mine stone for your tribe.
    on tick:yield 10 stone

Layout
  *main
    contains:res, buttons, achievs
    *res
      contains:Resources
      class:fullWidth
    *buttons
      contains:Buttons
    *achievs
      contains:Achievements
      class:fullWidth
  *store
    contains:buildings, upgrades, upgrBtns
    *buildings
      contains:BulkDisplay, Buildings
      header:Buildings
      tooltip origin:left
    *upgrades
      contains:Upgrades
      header:Upgrades
      costs:hide
      names:hide
    *upgrBtns
      contains:tag:upgradeSwitcher

Achievements
  *wellMilestoneOne
    name:Wishing Well
    desc:Have <b>one well</b>.
    req:1 well
  *farmerMilestoneOne
    name:Out on the farm
    desc:Have <b>one farmer</b>.
    req:1 farmer
  *farmerMilestoneTwo
    name:Reap what you sow
    desc:Have <b>25 farmers</b>.
    req:25 farmers
    on earn:toast You now have the ability to buy <b>fertilizer.</b> Each fertilizer you have multiplies the yield of farmers by two.

Upgrades
*TEMPLATE
  on earn:hide this
  *wellUpgrade1
    name:Deeper Wells
    desc:Wells are <b>twice as efficient.</b>
    cost:5 stone
    req:1 well
    passive:multiply yield of wells by 2
  *hunterUpgrade1
    name:Sharper Spears
    desc:Hunters are <b>twice as efficient.</b>
    cost:5 stone
    req:1 hunter
    passive:multiply yield of hunters by 2
*farmerUpgrade1
    name:Stronger Hoes
    desc:Farmers are <b>twice as efficient.</b>
    cost:5 stone
    req:1 farmer
    passive:multiply yield of farmers by 2
*farmerUpgradeWell
    name:Irrigation System
    desc:Farmers are <b>+1% efficient</b> for every well.
    cost:50 stone, 50 water
    req:5 farmers, 5 well
    passive:multiply yield of farmers by 0.01 * wells
*laborUpgrade1
    name:Intensive Labor
    desc:The labor button is <b>twice as efficient.</b>
    passive:multiply yield of workBtn by 2
    cost:10 stone

Items
  *fertilizer
    name:Fertilizer
    desc:Every one fertilizer makes farmers <b>twice as efficient.</b>
    passive:multiply yield of farmers by 2
